#def(eventHandlersProperty)({@str(Event.Functions)}>>)
#def(eventListProperty)({@str(Event.List)}>>)

#def(eventsInit)(
    $eventHandlersProperty
    $personal.get
    Gemini Decomposition
    Observation Purification
    Augur's Purification
    {
        Bookkeeper's Gambit: v
    }
    {
        $dictCreate
        Schrödinger's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit

    $eventListProperty
    $personal.get
    Gemini Decomposition
    Observation Purification
    Augur's Purification
    {
        Bookkeeper's Gambit: v
    }
    {
        Vacant Reflection
        Schrödinger's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
)

#def(addEventHandler)(
    // stack: event name, function
    $eventHandlersProperty
    $personal.get
    Numerical Reflection: -2
    Fisherman's Gambit II
    Observation Purification
    // stack: prop, name, function, list
    Rotation Gambit
    Dioscuri Gambit
    $dictRead
    // stack: prop, name, function, list, list2
    // stack: prop, function, list, name, list2
    Gemini Decomposition
    Augur's Purification
    {}
    {
        Bookkeeper's Gambit: v
        Vacant Reflection
    }
    Augur's Exaltation
    Hermes' Gambit
    // stack: prop, function, list, name, list2
    Numerical Reflection: 3
    Fisherman's Gambit
    Integration Distillation
    $dictOverwrite
    Schrödinger's Gambit
)

#def(removeEventHandler)(
    // stack: event name, function
    $eventHandlersProperty
    $personal.get
    Numerical Reflection: -2
    Fisherman's Gambit II
    Observation Purification
    // stack: prop, name, function, list
    Rotation Gambit
    Dioscuri Gambit
    $dictRead
    // stack: prop, name, function, list, list2
    // stack: prop, function, list, name, list2
    Gemini Decomposition
    Augur's Purification
    {
        // stack: prop, function, list, name, list2
        Gemini Decomposition
        Numerical Reflection: 4
        Fisherman's Gambit
        Locator's Distillation
        Excisor's Distillation

        // if not empty
        Gemini Decomposition
        Augur's Purification
        {
            $dictOverwrite
        }
        {
            Bookkeeper's Gambit: v
            $dictRemove
        }
        Augur's Exaltation
        Hermes' Gambit

        Schrödinger's Gambit
    }
    {
        Bookkeeper's Gambit: vvvvv
    }
    Augur's Exaltation
    Hermes' Gambit
)

#def(clearEventHandlers)(
    $eventHandlersProperty
    $personal.get
    $dictCreate
    Schrödinger's Gambit
)

#def(handleEvents)(
    $eventListProperty
    $personal.get
    Gemini Decomposition
    Observation Purification
    Gemini Decomposition
    Augur's Purification
    {
        // stack: prop, list
        Jester's Gambit
        Vacant Reflection
        Schrödinger's Gambit

        $eventHandlersProperty
        $personal.get
        Observation Purification
        // stack: event_list, func_list
        {
            // stack: func_list, event
            Sekhmet's Gambit: 2
            Flock's Disintegration
            $handleSingleEvent
            Sekhmet's Gambit: 0
        }
        Rotation Gambit
        Thoth's Gambit
    }
    {}
    Augur's Exaltation
    Hermes' Gambit
    Bookkeeper's Gambit: vv
)

#def(handleSingleEvent)(
    // stack: event function list, event name, param list
    // add event type at start of stack
    Prospector's Gambit
    Speaker's Distillation

    Rotation Gambit II
    $dictRead
    Gemini Decomposition
    Augur's Purification
    {
        {
            Sekhmet's Gambit: 2
            Hermes' Gambit
            Sekhmet's Gambit: 0
        }
        Jester's Gambit
        Thoth's Gambit
    }
    {
        // Unknown event
    }
    Augur's Exaltation
    Hermes' Gambit
    Bookkeeper's Gambit: vv
)

#def(triggerEvent)(
    // stack: event name, param list
    Numerical Reflection: 2
    Flock's Gambit
    $eventListProperty
    $personal.get
    Undertaker's Gambit
    Observation Purification
    Jester's Gambit
    Integration Distillation
    Schrödinger's Gambit
)

#def(clearEvents)(
    $eventListProperty
    $personal.get
    Vacant Reflection
    Schrödinger's Gambit
)


