// gets prop from personal prop in evo
// if dont exists, creates new prop and writes it to storage
// stack: name
#def(personalGet)($evocationPersonalRead)

// removes key from personal prop in evo
// stack: name
#def(personalRem)($evocationPersonalRemove)




#def(evocationGetProp)(
    // gets personal prop from evo
    Evocation Reflection
    {$personal_property}>>
    Locator's Distillation
    Numerical Reflection: -1
    Equality Distillation
    {
        {@str(Evo not set up. Terminating...)}>>
        Reveal
        Janus' Gambit
    }
    {}
    Augur's Exaltation
    Hermes' Gambit

    Evocation Reflection
    Derivation Decomposition
    Bookkeeper's Gambit: v-
)

#def(evocationPersonalWrit)(
    // sets value to personal prop in evo
    // stack: name, value
    $evocationGetProp
    Numerical Reflection: -2
    Fisherman's Gambit II
    Observation Purification
    Rotation Gambit II
    $dictOverwrite
    Schrödinger's Gambit
)

#def(evocationPersonalRead2)(
    // gets value from personal prop in evo
    // stack: name
    $evocationGetProp
    Observation Purification
    Jester's Gambit
    $dictRead
)

#def(evocationPersonalRead)(
    // gets prop from personal prop in evo
    // if dont exists, creates new prop and writes it to storage
    // stack: name
    $evocationGetProp
    Undertaker's Gambit
    Observation Purification
    Prospector's Gambit
    $dictRead
    Gemini Decomposition
    Augur's Purification
    // stack: prop, name, val
    {
        Bookkeeper's Gambit: vv-
    }
    {
        Bookkeeper's Gambit: v
        Prospector's Gambit
        Observation Purification
        Jester's Gambit
        Schrödinger's Reflection
        Numerical Reflection: -3
        Fisherman's Gambit II
        $dictOverwrite
        Schrödinger's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
)

#def(evocationPersonalRemove)(
    // removes key from personal prop in evo
    // stack: name
    $evocationGetProp
    Undertaker's Gambit
    Observation Purification
    Jester's Gambit
    $dictRemove
    Schrödinger's Gambit
)

#def(evocationSetPersonalProperty)(
    // adds prop at the end of evo
    // requires to replace null at pos 1 with prop
    {
        @null
        $personal_property
    }>>
    Evocation Reflection
    Jester's Gambit
    Locator's Distillation
    Numerical Reflection: -1
    Equality Distillation
    {
        Evocation Reflection
        {
            Janus' Gambit
            $personal_property
            @null
        }
        Rotation Gambit
        Numerical Reflection: 2
        Jester's Gambit
        Surgeon's Exaltation
        Additive Distillation
        Inculcate
    }
    {
        Evocation Reflection
        {$personal_property}>>
        Locator's Distillation
        Numerical Reflection: 1
        Additive Distillation
        Evocation Reflection
        Reflecting Gambit
        Surgeon's Exaltation
        Inculcate
    }
    Augur's Exaltation
    Hermes' Gambit
)