
#def(personal.names.functions)({@str(PersonalCashedFunctions)}>>)


#def(personal.property.get)($evocation.property.get)



#def(personal.functions.get)(
    // get function prop
    // stack: name
    $personal.names.functions
    $personal.get
    Observation Purification
    Jester's Gambit
    $dictRead
)

#def(personal.functions.set)(
    // sets function to prop from personal function prop
    // if prop already exists, overrides it, otherwise creates new prop
    // stack: name, function
    Jester's Gambit
    $personal.names.functions
    $personal.get
    Undertaker's Gambit
    Observation Purification
    Prospector's Gambit
    $dictRead
    Gemini Decomposition
    Augur's Purification
    // stack: prop, name, val
    {
        Bookkeeper's Gambit: vv-
    }
    {
        Bookkeeper's Gambit: v
        Prospector's Gambit
        Observation Purification
        Jester's Gambit
        Schrödinger's Reflection
        Numerical Reflection: -3
        Fisherman's Gambit II
        $dictOverwrite
        Schrödinger's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
    Jester's Gambit
    Schrödinger's Gambit
)

#def(personal.functions.remove)(
    // removes key from personal prop function cashe
    // stack: name
    $personal.names.functions
    $personal.get
    Undertaker's Gambit
    Observation Purification
    Jester's Gambit
    $dictRemove
    Schrödinger's Gambit
)

#def(personal.functions.init)(
    $personal.names.functions
    $personal.get
    Gemini Decomposition
    Observation Purification
    Augur's Purification
    {
        Bookkeeper's Gambit: v
    }
    {
        $dictCreate
        Schrödinger's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
)




#def(personal.set)(
    // sets value to personal prop
    // stack: name, value
    $personal.property.get
    Numerical Reflection: -2
    Fisherman's Gambit II
    Observation Purification
    Rotation Gambit II
    $dictOverwrite
    Schrödinger's Gambit
)

#def(personal.read)(
    // gets value from personal prop
    // stack: name
    $personal.property.get
    Observation Purification
    Jester's Gambit
    $dictRead
)

#def(personal.get)(
    // gets prop from personal prop
    // if dont exists, creates new prop and writes it to storage
    // stack: name
    $personal.property.get
    Undertaker's Gambit
    Observation Purification
    Prospector's Gambit
    $dictRead
    Gemini Decomposition
    Augur's Purification
    // stack: prop, name, val
    {
        Bookkeeper's Gambit: vv-
    }
    {
        Bookkeeper's Gambit: v
        Prospector's Gambit
        Observation Purification
        Jester's Gambit
        Schrödinger's Reflection
        Numerical Reflection: -3
        Fisherman's Gambit II
        $dictOverwrite
        Schrödinger's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
)

#def(personal.remove)(
    // removes key from personal prop
    // stack: name
    $personal.property.get
    Undertaker's Gambit
    Observation Purification
    Jester's Gambit
    $dictRemove
    Schrödinger's Gambit
)

#def(personal.init)(
    $personal.property.get
    Gemini Decomposition
    Observation Purification
    Augur's Purification
    {
        Bookkeeper's Gambit: v
    }
    {
        $dictCreate
        Schrödinger's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit

    $personal.functions.init
)