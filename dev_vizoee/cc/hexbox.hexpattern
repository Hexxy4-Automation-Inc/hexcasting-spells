#git(/libs/base.hexpattern)

#def(mem_pos)({@vec(-394, 56, -697)}>>)
#def(in_prop_pos)({@vec(-393, 58, -697)}>>)
#def(out_prop_pos)({@vec(-394, 58, -697)}>>)
#def(focal_port_pos)({@vec(-393, 56, -697)}>>)

#def(cc_request)(@str(hexget))

#def(key_mode)(@str(mode))
#def(key_id)(@str(id))
#def(key_request)(@str(request))
#def(key_hash)(@str(hash))
#def(key_response)(@str(response))

#def(key_val)(@str(value))

#def(key_in_prop)(@str(in_prop))
#def(key_out_prop)(@str(out_prop))
#def(key_mem_prop)(@str(mem_prop))

#def(mode_wait_for_request)(@str(w_req))
#def(mode_send_request)(@str(req))
#def(mode_wait_for_response)(@str(w_res))
#def(mode_send_response)(@str(res))

#def(create_map)(
    // param: mode
    @pattern(EAST,dqdwdqd)
    {
        $key_mode
        <1>
    }>>
    Surgeon's Exaltation
)

#def(get_mem_val)(
    Muninn's Reflection
    {<1>}>>
    Selection Distillation
)

#def(wait_for_request)({
    $get_mem_val($key_in_prop)
    Gemini Decomposition
    Observation Purification

    Gemini Decomposition
    Length Purification
    Augur's Purification
    {
        Derivation Decomposition
        // prop, key, val
        Muninn's Reflection
        {$key_val}>>
        Rotation Gambit
        Surgeon's Exaltation

        {$key_id}>>
        Rotation Gambit
        Surgeon's Exaltation
        
        {
            $key_mode
            $mode_send_request
        }>>
        Surgeon's Exaltation
        $get_mem_val($key_mem_prop)
        Jester's Gambit
        Schrödinger's Gambit
        // stack: prop, m1
        Schrödinger's Gambit
    }
    {
        Janus' Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
})

#def(send_request)({
    Muninn's Reflection
    {$key_val}>>

    $focal_port_pos
    Entity Purification
    {$cc_request}
    $get_mem_val($key_val)
    Integration Distillation
    Undertaker's Gambit
    Chronicler's Gambit

    Hashing Purification
    Surgeon's Exaltation
    
    {
        $key_mode
        $mode_wait_for_response
    }>>
    Surgeon's Exaltation
    $get_mem_val($key_mem_prop)
    Jester's Gambit
    Schrödinger's Gambit
})

#def(wait_for_response)({
    $focal_port_pos
    Entity Purification
    Chronicler's Purification
    Hashing Purification

    $get_mem_val($key_val)
    Equality Distillation
    {
        Janus' Gambit
    }
    {
        Muninn's Reflection
        {$key_val}>>
        $focal_port_pos
        Entity Purification
        Chronicler's Purification
        Surgeon's Exaltation

        {
            $key_mode
            $mode_send_response
        }>>
        Surgeon's Exaltation
        $get_mem_val($key_mem_prop)
        Jester's Gambit
        Schrödinger's Gambit

        // Maybe clean focus here
        $focal_port_pos
        Entity Purification
        {}
        Chronicler's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
})

#def(send_response)({
    $get_mem_val($key_out_prop)
    Gemini Decomposition
    Observation Purification
    $get_mem_val($key_id)
    $get_mem_val($key_val)
    Surgeon's Exaltation
    Schrödinger's Gambit
    
    Muninn's Reflection
    $get_mem_val($key_id)
    Excisor's Distillation
    $get_mem_val($key_val)
    Excisor's Distillation
    {
        $key_mode
        $mode_wait_for_request
    }>>
    Surgeon's Exaltation
    $get_mem_val($key_mem_prop)
    Jester's Gambit
    Schrödinger's Gambit
})

#def(init)(
    @pattern(EAST,dqdwdqd)

    {
        $mode_wait_for_request
    }>>
    $wait_for_request
    Surgeon's Exaltation

    {
        $mode_send_request
    }>>
    $send_request
    Surgeon's Exaltation

    {
        $mode_wait_for_response
    }>>
    $wait_for_response
    Surgeon's Exaltation

    {
        $mode_send_response
    }>>
    $send_response
    Surgeon's Exaltation

    $cText(Bubbled commands :3)(@vec(0.65, 1, 0.4))

    {@vec(0, 0, 0)}>>

    $akashaVar(lib/map/prop/bubble)
    @pattern(SOUTH_EAST,deaqqwqqqeaeqqqeadedaqaaee)

    Single's Purification

    $mem_pos
    Chronicler's Purification
    Bubbles' Purification
    Single's Purification

    {
        Observation Purification
        Huginn's Gambit
        
        $get_mem_val($key_mode)
        Selection Distillation
        Hermes' Gambit
    }
    Additive Distillation
    Additive Distillation
)

#def(mem_init)(
    $mem_pos
    Chronicler's Purification
    $create_map($mode_wait_for_request)

    {$key_in_prop}>>
    $akashaVar(lib/cc/request_prop)
    Surgeon's Exaltation
    
    {$key_out_prop}>>
    $akashaVar(lib/cc/response_prop)
    Surgeon's Exaltation

    Prospector's Gambit
    {$key_mem_prop}>>
    Jester's Gambit
    Surgeon's Exaltation

    Schrödinger's Gambit
)


$init
$mem_init