/*
Input: source package, destination position, column axis, package look direction

Takes package from package storage and places it in given position in same orientation as pacakge storage. 
Column axis is orientation in which package storage is set up.
*/

/*
simplified:
    get package
    if can't break it and give it
    if broken use 'get' to replace destroyed package

detailed:
input: source, destination, column_axis, package_look_direction

checks if 0 slot is empty:
    if not move 1 package from source and place it at destination
        (use package_look_direction for correct orientation and will look same direction as pacakge storage)
    if yes:
    break package at source and place it at destination (use package_look_direction for correct orientation)
    add next iteration of 'get' where new destination is current source 
        and new source is current source + column_axis
*/

#def(place)(
    // stack: item, pos, look
    Undertaker's Gambit
    Subtractive Distillation
    Jester's Gambit
    // stack: item, dispenser_pos, look
    Dispense
)

// stack: src, dst, axis, look
Numerical Reflection: 4
Flock's Gambit
{
    Flock's Disintegration
    // stack: src, dst, axis, look
    Numerical Reflection: 3
    Fisherman's Gambit II
    Hopper Purification
    Numerical Reflection: 0
    Selection Distillation
    Augur's Purification
    {// not empty
        // stack: src, dst, axis, look
        Bookkeeper's Gambit: v-
        // stack: src, dst, look
        Rotation Gambit II
        // stack: look, src, dst
        Dioscuri Gambit
        Hopper
        // stack: look, src, dst
        Bubbling Gambit
        // stack: src, look, dst
        Undertaker's Gambit
        Entity Purification: Item
        // stack: src, dst, look, item
        Numerical Reflection: -2
        Fisherman's Gambit II
        Rotation Gambit II
        $place
        // stack: src, item
        Gemini Decomposition
        Augur's Purification
        {
            Jester's Gambit
            Hopper
        }
        {}
        Augur's Exaltation
        Hermes' Gambit
    }
    {// empty
        // move package from source to dest
        // stack: src, dst, axis, look
        Numerical Reflection: 3
        Fisherman's Gambit II
        // stack: src, dst, axis, look, src
        Gemini Decomposition
        Break Block
        Entity Purification: Item
        // stack: src, dst, axis, look, item
        Numerical Reflection: 3
        Fisherman's Gambit
        // stack: src, axis, look, item, dst
        Numerical Reflection: 2
        Fisherman's Gambit II
        // stack: src, axis, look, item, dst, look
        $place
        // stack: src, axis, look
        Rotation Gambit II
        Dioscuri Gambit
        Additive Distillation
        Jester's Gambit
        // stack: look, src->dst, new_src, axis
        Bubbling Gambit
        Numerical Reflection: 3
        Fisherman's Gambit
        // stack: new_src, src->dst, axis, look
        
        // run get with src+axis, src, axis, look
        Numerical Reflection: 4
        Flock's Gambit
        Castor's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
}
Jester's Gambit
Single's Purification
Thoth's Gambit
