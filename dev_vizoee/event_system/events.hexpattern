#git(/libs/base.hexpattern)

#def(eventHandlersProperty)({@str(Event.Functions)}>>)
#def(eventListProperty)({@str(Event.List)}>>)

#def(addEventHandler)(
    $print(addEventHandler)
    // stack: event name, function
    $eventHandlersProperty
    $personalGet
    Numerical Reflection: -2
    Fisherman's Gambit II
    Observation Purification
    // stack: prop, name, function, list
    Rotation Gambit
    Dioscuri Gambit
    $dictRead
    // stack: prop, name, function, list, list2
    // stack: prop, function, list, name, list2
    Gemini Decomposition
    Augur's Purification
    {}
    {
        Bookkeeper's Gambit: v
        Vacant Reflection
    }
    Augur's Exaltation
    Hermes' Gambit

    $print(addEventHandler 2)
    $stackPrint
    $print(addEventHandler 3)
    
    
    // stack: prop, function, list, name, list2
    Numerical Reflection: 3
    Fisherman's Gambit
    Integration Distillation
    $dictOverwrite
    Schrödinger's Gambit
)

#def(clearEventHandlers)(
    $print(clearEventHandlers)
    $eventHandlersProperty
    $personalGet
    $dictCreate
    Schrödinger's Gambit
)

#def(handleEvents)(
    $print(handleEvents)
    $eventListProperty
    $personalGet
    Gemini Decomposition
    Observation Purification
    Gemini Decomposition
    Augur's Purification
    {
        $print(New Events)
        // stack: prop, list
        Jester's Gambit
        Vacant Reflection
        Schrödinger's Gambit

        $eventHandlersProperty
        $personalGet
        Observation Purification
        // stack: event_list, func_list
        {
            // stack: func_list, event
            Sekhmet's Gambit: 2
            Flock's Disintegration
            $handleSingleEvent
            Sekhmet's Gambit: 0
        }
        Rotation Gambit
        $stackPrint
        Thoth's Gambit
    }
    {
        $print(No Events)
    }
    Augur's Exaltation
    Hermes' Gambit
    Bookkeeper's Gambit: vv
)

#def(handleSingleEvent)(
    $print(handleSingleEvent)
    // stack: event function list, event name, param list
    Rotation Gambit II
    $dictRead
    {
        Sekhmet's Gambit: 2
        {
            Flock's Disintegration
        }
        Jester's Gambit
        Additive Distillation
        Hermes' Gambit
        Sekhmet's Gambit: 0
    }
    Jester's Gambit
    Thoth's Gambit
    Bookkeeper's Gambit: v
)

#def(triggerEvent)(
    $print(triggerEvent)
    // stack: event name, param list
    Numerical Reflection: 2
    Flock's Gambit
    $eventListProperty
    $personalGet
    Undertaker's Gambit
    Observation Purification
    Jester's Gambit
    Integration Distillation
    Schrödinger's Gambit
)

#def(clearEvents)(
    $print(clearEvents)
    $eventListProperty
    $personalGet
    $dictCreate
    Schrödinger's Gambit
)


